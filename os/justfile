# Log Level
export LOG := "DEBUG"

# Target architecture
target := "riscv64gc-unknown-none-elf"
mode := "release"

# Board and bootloader
board := "qemu"
sbi := "rustsbi"
bootloader := "../bootloader" / sbi + "-" + board + ".bin"

# Paths for kernel binary and disassembly
kernel_entry_pa := "0x80200000"
kernel_elf := "target" / target / mode / "os"
kernel_bin := kernel_elf + ".bin"
disasm_tmp := kernel_elf + ".asm"

# User applications and filesystem image paths
user_dir := "../user"
app_dir := user_dir / "src/bin"
app_target_dir := user_dir / "target" / target / mode

# Tools for handling object files
objdump := "rust-objdump --arch-name=riscv64"
objcopy := "rust-objcopy --binary-architecture=riscv64"

# QEMU
bootloader_option := "-bios " + bootloader
display_option := "-nographic"
machine_option := "-machine virt"
loader_option := "-device loader,file=" + kernel_bin + ",addr=" + kernel_entry_pa
qemu_args := machine_option + " " + display_option + " " + bootloader_option + " " + loader_option


# List available recipes
default:
    just --list

# Generate link_app.S
gen_app_link_asm:
    #!/usr/bin/env python3
    import os

    apps = os.listdir("{{app_dir}}")
    apps.sort()

    app_num = f"""
        .align 3
        .section .data
        .global _num_app
    _num_app:
        .quad {len(apps)}
    """
    app_names = f"""
        .global _app_names
    _app_names:
    """
    app_data = ""

    for app_id, app in enumerate(apps):
        base = os.path.splitext(app)[0]
        app_num += f"    .quad app_{app_id}_start\n"
        app_names += f'    .string "{base}"\n'
        app_data += f"""
        .section .data
        .global app_{app_id}_start
        .global app_{app_id}_end
        .align 3
    app_{app_id}_start:
        .incbin "{{app_target_dir}}/{base}"
    app_{app_id}_end:
    """

    app_num += f"    .quad app_{len(apps) - 1}_end\n"

    with open("src/link_app.S", "w") as f:
        f.write(app_num)
        f.write(app_names)
        f.write(app_data)

# Build the kernel binary
build: gen_app_link_asm 
    cd {{user_dir}} && just build
    @ cp "src/linker-{{board}}.ld" "src/linker.ld"
    cargo build --{{mode}}
    @ rm src/linker.ld
    @ rm src/link_app.S
    {{objcopy}} {{kernel_elf}} --strip-all -O binary {{kernel_bin}}

# Run in QEMU
run: build
    qemu-system-riscv64 {{qemu_args}}

# Debug the kernel in QEMU using tmux
debug: build
    tmux new-session -d "qemu-system-riscv64 {{qemu_args}} -s -S"
    tmux split-window -h "gdb-multiarch -ex 'file {{kernel_elf}}' -ex 'target remote localhost:1234'"
    tmux -2 attach-session -d

# Start a GDB server for debugging
gdbserver: build
    qemu-system-riscv64 {{qemu_args}} -s -S

# Connect to the GDB server
gdbclient:
    gdb-multiarch -ex "file {{kernel_elf}}" -ex "target remote localhost:1234"

# Clean build artifacts
clean:
    cargo clean

# Disassemble for inspection using bat
disasm: build
    {{objdump}} -d {{kernel_elf}} | bat -l asm

# Disassemble for inspection using LunarVim
disasm-vim: build
    {{objdump}} -d {{kernel_elf}} > {{disasm_tmp}}
    lvim {{disasm_tmp}}
    rm {{disasm_tmp}}
